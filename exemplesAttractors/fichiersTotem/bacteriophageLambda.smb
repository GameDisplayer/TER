# Formal Methods for Modeling Biological Regulatory Networks. In Modern Formal Methods and Applications (eds. H.A. Gabbar). pp. 83-122, ISBN: 1-4020-4222-1, Springer, 2006.

VAR
cI = 0 .. 2;
cro = 0 ..3;
cII = 0.. 1;
N = 0 ..1;

REG

inhicI [!(cro>=1)]	=> cI ;
acticI [cII>=1] 	=> cI;
autocI [cI>=2] 		=> cI;

inhicro [!(cI>=2)]	=> cro ;
autocro [!(cro>=3)]	=> cro ;

inhicII1 [!(cI>=2)]	=> cII ;
inhicII2 [!(cro>=3)]	=> cII ;
acticII [N>=1]		=> cII ;

inhiN1 [!(cI>=1)]	=> N;
inhiN2 [!(cro>=2)]	=> N;

PARA

K_cI = 0;
K_cI:autocI = 1 ..2;
#K_cI:inhicI = 0 ..2;
#K_cI:acticI = 0 ..2;
K_cI:autocI:inhicI = 2;
#K_cI:acticI:autocI = 1 ..2;
K_cI:inhicI = 2;
K_cI:acticI:autocI:inhicI = 2;

K_cII = 0;
K_cII:inhicII1 = 0;
K_cII:inhicII2 = 0;
K_cII:acticII = 0;
#K_cII:inhicII1:inhicII2 = 0.. 1;
#K_cII:acticII:inhicII1 = 0.. 1;
#K_cII:acticII:inhicII2 = 0.. 1;
K_cII:acticII:inhicII1:inhicII2 = 1;

K_cro = 0;
K_cro:inhicro = 2;
K_cro:autocro = 0;
#K_cro:autocro:inhicro  = 2.. 3;

K_N  = 0;
K_N:inhiN1 = 0;
K_N:inhiN2 = 0;
K_N:inhiN1:inhiN2 = 1;

CTL
# lytic => AG(lytic)
(((cI = 0) & (cro >= 2) & (cII = 0) & (N = 0)) -> AG((cI = 0) & (cro >= 2) & (cII = 0) & (N = 0)));

# immune => AG(immune)
CTL
(((cI = 2) & (cro = 0) & (cII = 0) & (N = 0)) -> AG((cI = 2) & (cro = 0) & (cII = 0) & (N = 0)));

# init ⇒ ( EF(lytic) ∧ EF(immune) )
#CTL
(((cI = 0) & (cro = 0) & (cII = 0) & (N = 0)) -> (EF((cI = 0) & (cro >= 2) & (cII = 0) & (N = 0)) & EF((cI = 2) & (cro = 0) & (cII = 0) & (N = 0)))) ;

END